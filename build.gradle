buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/libs-milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.3.0.RELEASE")
        classpath("au.com.dius:pact-jvm-provider-gradle:4.0.3")
    }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'

  repositories {
     mavenCentral()
  }
}

subprojects {
  apply plugin: 'org.springframework.boot'

  sourceCompatibility = 11
  targetCompatibility = 11

  dependencies {
      implementation("org.springframework.boot:spring-boot-starter-web:2.3.0.RELEASE")
      implementation("org.springframework.boot:spring-boot-starter-thymeleaf:2.3.0.RELEASE")
      testImplementation('org.springframework.boot:spring-boot-starter-test2.3.0.RELEASE')
      testImplementation("junit:junit:4.13")
      testImplementation("au.com.dius:pact-jvm-consumer-junit:4.0.3")
      testImplementation("au.com.dius:pact-jvm-consumer-java8:4.0.3")
  }

}

project(':example-consumer-miku') {
    version '1.0.0'
    apply plugin: 'au.com.dius.pact'

    jar {
        archiveBaseName = 'example-consumer-miku'
        archiveVersion =  '1.0.0'
    }

    test {
        systemProperties['pact.rootDir'] = "$rootDir/Pacts/Miku"
    }

    pact {
        publish {
            pactDirectory = "$rootDir/Pacts/Miku"

            pactBrokerUrl = mybrokerUrl
            pactBrokerUsername = mybrokerUser
            pactBrokerPassword = mybrokerPassword

            // This is to local Pact Broker in Docker
//            pactBrokerUrl = "http://localhost"
        }
    }
}

project(':example-consumer-nanoha') {
    jar {
        archiveBaseName = 'example-consumer-nanoha'
        archiveVersion =  '1.0.0'
    }

    test {
        systemProperties['pact.rootDir'] = "$rootDir/Pacts/Nanoha"
    }
}

import java.net.URL

project(':example-provider') {
    apply plugin: 'au.com.dius.pact'

    jar {
        archiveBaseName = 'example-provider'
        archiveVersion =  '1.0.0'
    }

    pact {
        serviceProviders {
            ExampleProvider {
                protocol = 'http'
                host = 'localhost'
                port = 8080
                path = '/'

                // Test Pacts from local Miku
                hasPactWith('Miku - Base contract') {
                    pactSource = file("$rootDir/Pacts/Miku/BaseConsumer-ExampleProvider.json")
                }

                hasPactsWith('Miku - All contracts') {
                    pactFileLocation = file("$rootDir/Pacts/Miku")
                }

                // Test Pacts from Pact Broker
                hasPactsFromPactBroker(mybrokerUrl, authentication: ['Basic', mybrokerUser, mybrokerPassword])

                // This is for local Pact Broker in Docker
                //hasPactsFromPactBroker("http://localhost")

                // Test Pacts from local Nanoha
//                hasPactWith('Nanoha - With Nantionality') {
//                    pactSource = file("$rootDir/Pacts/Nanoha/ConsumerNanohaWithNationality-ExampleProvider.json")
//                }

//                hasPactWith('Nanoha - No Nantionality') {
//                    stateChangeUrl = new URL('http://localhost:8080/pactStateChange')
//                    pactSource = file("$rootDir/Pacts/Nanoha/ConsumerNanohaNoNationality-ExampleProvider.json")
//                }

            }
        }
    }

}


